{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE ScopedTypeVariables #-}

module Cardano.Tracer.Handlers.RTView.UI.HTML.Node.Column
  ( addNodeColumn
  , deleteNodeColumn
  ) where

import           Control.Monad (forM, void)
import           Control.Monad.Extra (whenJustM)
import           Data.List.NonEmpty (NonEmpty)
import qualified Data.List.NonEmpty as NE
import           Data.Text (unpack)
import qualified Graphics.UI.Threepenny as UI
import           Graphics.UI.Threepenny.Core
import           System.FilePath ((</>))

import           Cardano.Tracer.Configuration
import           Cardano.Tracer.Handlers.RTView.UI.JS.Utils
import           Cardano.Tracer.Handlers.RTView.UI.Img.Icons
import           Cardano.Tracer.Handlers.RTView.UI.Utils
import           Cardano.Tracer.Types

-- | For every connected node the new column should be added.
addNodeColumn
  :: UI.Window
  -> NonEmpty LoggingParams
  -> NodeId
  -> UI ()
addNodeColumn window loggingConfig (NodeId anId) = do
  let id' = unpack anId
  ls <- logsSettings loggingConfig id'
  addNodeCell "name"     [ UI.span ## (id' <> "__node-name")
                                   #. "has-text-weight-bold is-size-4"
                                   # set text "Node"
                         ]
  addNodeCell "version"  [ UI.span ## (id' <> "__node-version")
                                   # set text "—"
                         ]
  addNodeCell "protocol" [ UI.span ## (id' <> "__node-protocol")
                                   # set text "—"
                         ]
  addNodeCell "commit"   [ UI.anchor ## (id' <> "__node-commit")
                                     #. ("rt-view-href is-family-monospace has-text-weight-normal"
                                         <> " has-tooltip-multiline has-tooltip-right")
                                     # set UI.href "#"
                                     # set dataTooltip "Browse cardano-node repository on this commit"
                                     # set text "—"
                         ]
  addNodeCell "start-time"        [ UI.span ## (id' <> "__node-start-time")
                                            # set text "—"
                                  ]
  addNodeCell "system-start-time" [ UI.span ## (id' <> "__node-system-start-time")
                                            # set text "—"
                                  ]
  addNodeCell "uptime"   [ UI.span ## (id' <> "__node-uptime")
                                   # set text "—"
                         ]
  addNodeCell "logs"     [ UI.span ## (id' <> "__node-logs")
                                   #+ ls -- logsSettings loggingConfig anId
                         ]
  addNodeCell "peers"    [ UI.span ## (id' <> "__node-peers")
                                   # set text "—"
                         ]
  addNodeCell "chain"    [ UI.span ## (id' <> "__node-chain")
                                   # set text "—"
                         ]
  addNodeCell "errors"   [ UI.span ## (id' <> "__node-errors")
                                   # set text "No errors"
                         ]
 where
  addNodeCell rowId cellContent =
    whenJustM (UI.getElementById window ("node-" <> rowId <> "-row")) $ \el ->
      void $ element el #+ [ UI.th #. (unpack anId <> "__column_cell rt-view-node-column-cell")
                                   #+ cellContent
                           ]

-- | The new node is already connected, so we can display its logging settings.
logsSettings
  :: NonEmpty LoggingParams
  -> String
  -> UI [UI Element]
logsSettings loggingConfig anId =
  forM (NE.toList loggingConfig) $ \(LoggingParams root mode format) ->
    case mode of
      FileMode -> do
        let pathToSubdir = root </> anId
        copyPath <- image "has-tooltip-multiline has-tooltip-top rt-view-copy-icon" copySVG
                          # set dataTooltip "Click to copy the path to a directory with logs from this node"
        on UI.click copyPath . const $
          UI.runFunction $ UI.ffi copyTextToClipboard pathToSubdir
        return $ UI.p #+
                   [ UI.span #. ("tag is-info is-light is-rounded mr-3"
                                 <> " has-tooltip-multiline has-tooltip-top")
                             # set dataTooltip (pathToSubdir
                                                <> " is the path to a directory with logs from this node")
                             # set text (shorten pathToSubdir)
                   , element copyPath
                   , UI.span #. ("tag is-warning is-light is-rounded ml-3 "
                                 <> "has-tooltip-multiline has-tooltip-top")
                             # set dataTooltip "The format log files are written in"
                             # set text (if format == ForHuman then "LOG" else "JSON")
                   ]
      JournalMode -> do
        copyId <- image "has-tooltip-multiline has-tooltip-top rt-view-copy-icon" copySVG
                        # set dataTooltip "Click to copy the syslog identifier of this node"
        on UI.click copyId . const $
          UI.runFunction $ UI.ffi copyTextToClipboard anId
        return $ UI.p #+
                   [ UI.span #. ("tag is-info is-light is-rounded mr-3"
                                 <> " has-tooltip-multiline has-tooltip-top")
                             # set dataTooltip (anId <> " is the syslog identifier for this node")
                             # set text (shorten anId)
                   , element copyId
                   , UI.span #. ("tag is-warning is-light is-rounded ml-3 "
                                 <> "has-tooltip-multiline has-tooltip-top")
                             # set dataTooltip "Logs from this node are written in systemd's journal"
                             # set text "JRNL"
                   ]
 where
  shorten t =
    if length t > 20
      then take 20 t <> "..."
      else t

-- | The node was disconnected, so its column should be deleted.
deleteNodeColumn
  :: UI.Window
  -> NodeId
  -> UI ()
deleteNodeColumn window (NodeId anId) = do
  let className = anId <> "__column_cell"
  findByClassAndDo window className UI.delete
